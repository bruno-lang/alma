% bruno grammar in alma %

% ///////////////////////////////[ Fragments ]/////////////////////////////////%

% special symbols %
-Optional     = '?'
-Faulty       = '!'
-Transient    = '*'
-Self         = '~'
-Wildcard     = '_'
-InQuestion   = '?'

% basic words %
-Abbr         = \Z \9?
-Noun         = \Z [zZ '-']+ [zZ9 '!']* | '^' ['+-'9] \9*
-Verb         = \z [z9 '-']* [z9]? ['?!' a]?
-Item         = [z '?']'.'
-Atom         = '`' \s^+
-Key          = '@' [Zs ',']^ [s ',{}()[]']^*
-Var          = '?' [zZ9 '-_' ]+
-Number       = ['+-']? \9+ ([',_'] \9+)* ('.' \9+)?

% tags %
-label        = Verb ':'
-step         = Noun ':'
-name         = Noun | Key
-abstraction  = Noun (; Abbr)*

% Operators %
-Op           = ['-+/*=><&']
-Operator     = \Op [\Op '!?|']{0-2} | [\Op '|']{2-3}

% Literals %
-literal      = textual | numerical
-textual      = '"""' <       ~@TEXT '"""'
              | '"'   < ['"']^*@TEXT '"' 
              | \a    <    \a^*@TEXT \a
-numerical    = Number (Number | [s'^%+-*!?#=~&|<>()[]{}.,;\"`'a]^)*

% /////////////////////////////////[ Types ]///////////////////////////////////%

-T            = '[]' | '{}' | '()' | Self | Atom | Key | T_sum ( . '&' . T_sum)*
-T_sum        = (. '+' ,)? T_wide (. '+' , T_wide)*
-T_wide       = T_base dimension* variant?
-T_base       = T_ordinal | T_key   | T_list   | T_set  | T_tuple | T_fn 
              | Wildcard  | T_range | T_scalar | T_many
-T_many       = '..'
-T_key        = '@' < T
-T_ordinal    = '#' < T
-T_list       = '[' < T ']'
-T_set        = '{' < T '}'
-T_fn         = '(' (. T, Verb?, '->')+    , T    ')'
-T_tuple      = '(' (, ['\;,']@X? . label? , T )+ ')'
-T_range      = T_scalar@MIN '..' < T_scalar@MAX
-T_scalar     = '*' | Noun | Abbr | literal
-T_length     = T_range | T_scalar

-dimension    = D_subset | D_manual | D_slice | D_channel | D_array
-D_subset     = '{'  < T_length ('|' < T_length)* '}'
-D_slice      = '[{' < T_length '}]'
-D_array      = '['  < T_length  ']'
-D_manual     = '[!' < T_scalar  ']'
-D_channel    = ['[]'] ['<>']{1-2} < ['[]']

-variant      = [\Faulty \Optional \Transient]

% ////////////////////////[ Building Blocks ]//////////////////////////////////%

% Expressions %
-expr         = term ( , term )*
-term         = expand   | tuple | list | set  | comment | implicit | inline 
              | Wildcard | Atom  | Key  | literal 
              | Operator | Var   | Verb | Noun | Abbr | InQuestion

-inline       = '\' Verb@FUNCTION
-expand       = '(:=' < \form  ')'
-implicit     = '\('  < \form  ')'
-tuple        = '('   < \form? ')'
-list         = '['   < \form? ']'
-set          = '{'   < \form? '}'
-comment      = '%'   <   ~    '%'
-form         = . expr ( . ','? . expr )* .

% Principles (these use ::) %
-module       = ( . \principle )+ .
-principle    = def | data    | when  | fn | family | sub
              | op  | concept | fault | on | ratio  | io  | comment

-fault        = 'fault'   < ; Noun,        '::' . T !
-ratio        = 'ratio'   < ; Noun,        '::' . literal,'=' , literal !
-def          = 'def'     < ; name,        '::' .         '=' . expr !
-io           = 'io'      < ; Noun,        '::' . T
-data         = 'data'    < ; name, attrs  '::' . T   ( . '{' . constants . '}')? (. '='   < . expr)?
-family       = 'family'  < ; Abbr, attrs  '::' . T   ( . '=' , abstractions   )? (. ':&:' < , ops )?
-concept      = 'concept' < ; abstraction, '::' .         '=' . set
-op           = 'op'      < ; Verb, attrs  '::' . T_fn
-fn           = 'fn'      < ; Verb, attrs  '::' . T_fn? . '=' case+                 ( ':?:' < (. variable)+)?
-sub          = 'sub'     < ; Verb, attrs  '::' . T_fn? . '=' case+                 ( ':?:' < (. variable)+)?
-when         = 'when'    < ; Noun,        '::' . T_fn  . effect* '..', transition* ( ':?:' < (. variable)+)?
-on           = 'on'      < ; Noun,        '::' . T_fn  . effect* '..', transition* ( ':?:' < (. variable)+)?

% lists of ... %
-attrs        = (Atom ; )* 
-ops          = Verb@OP     ( , ',' < , Verb@OP     )*
-abstractions = abstraction ( , ',' < , abstraction )*
-constants    = constant    ( . '|' < . constant    )* 

% assignments ... %
-constant     = '..' | name ( , ':' < , T)? (. '=' < . expr)?
-variable     =        Verb ( , ':' < , T)? , ('=' < . expr ! | ( . '|=' < , expr ! )+ )

% implementation body %
-case         =       . Item? , expr (, '|', expr)* !
-transition   =         step  , expr (, '|', expr)* !
-effect       = \9'.' , step? , expr !

