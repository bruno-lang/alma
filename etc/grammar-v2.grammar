%v2
grammar       : member (, member)*
member        : comment | rule
comment       : '%' (!\n+):text
rule          : name, ('=' | (':' ':'? '='?)), selection ';'?
selection     : sequence (, '|' >> sequence )*
sequence      : element ( >> element )*
element       : (completion | group | option | terminal | symbol | literal | ref ) occurrence?
completion    : '..'
group         : '(', selection, ')' capture
option        : '[', selection, ']' capture

occurrence    : 'x'? num:min {'-' '+'}:to? num:max? | qmark | star | plus
num           : #+
qmark         : '?'
star          : '*'
plus          : '+'

ref           : name capture
name          : '-'? '\'? {@ # '_' '-'}+
capture       : [':' name:alias ]

symbol        : ''' !'''x2+ '''

wildcard      : '.'
atom          : ''' . '''
hexcode       : '\u' Xx4-8
literal       : hexcode | atom
range         : literal, '-', literal
letter        : '@'
digit         : '#'
hex           : 'X'
not           : {'!' '^'}
whitespace    : '_'
gap           : ','
pad           : '~'
indent        : '>>'

t             : '\t'
n             : '\n'
r             : '\r'
s             : '\s'
shortname     : s | t | n | r

class         : '\u{' @+ '}'
figure        : not? (wildcard | letter | hex | digit | class | range | literal | whitespace | shortname )
figure1       : figure | ref
figures       : '{', -figure1 (, -figure1 )* '}' capture
terminal      : figure | figures | gap | pad | indent