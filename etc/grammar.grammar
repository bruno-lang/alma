% this grammar is the grammar's grammar in BNF
grammar    : member+;
member     : comment
           | rule;
comment    : '%' !LF;
rule       : name ':' parts ';';
parts      : [ part occurrence? ] (else? parts)?; 
part       : group
           | token
           | atom;
token      : '[' parts ']';
group      : '(' parts ')';
atom       : not 
           | any
           | whitespace
           | range
           | terminal
           | name;
not        : [ '!' atom ];
terminal   : [ ''' .+ ''' ];
range      : terminal '-' terminal;
occurrence : [ '{' num ',' num '}' ]
           | qmark
           | star
           | plus
           | ellipsis;
num        : digit+;
digit      : '0' - '9';
qmark      : '?';
star       : '*';
plus       : '+';
ellipsis   : '..';
any        : '.';
whitespace : '_';
else       : '|';
character  : digit | 'a' - 'z' | 'A'-'Z' | '_' | '-';
name       : [ character+ ];